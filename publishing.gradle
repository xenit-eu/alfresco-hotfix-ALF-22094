publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact amp

            pom {
                url = "https://github.com/xenit-eu/${rootProject.getName()}"
                name = project.name
                description = project.description

                scm {
                    connection = "scm:git:git@github.com:xenit-eu/${rootProject.getName()}.git"
                    developerConnection = "scm:git:git@github.com:xenit-eu/${rootProject.getName()}.git"
                    url = "https://github.com/xenit-eu/${rootProject.getName()}"
                }
                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.hasProperty('de_publish_username') ? project.de_publish_username : ''
                password = project.hasProperty('de_publish_password') ? project.de_publish_password : ''
            }
        }
    }
}