plugins {
    id 'eu.xenit.alfresco' version "1.1.0" apply(false) // Have a look at https://plugins.gradle.org/plugin/eu.xenit.alfresco for the latest version
    id 'eu.xenit.amp' version "1.1.0" apply(false)
    id "eu.xenit.docker-alfresco" version "5.5.0" apply(false) // See https://plugins.gradle.org/plugin/eu.xenit.docker-alfresco for the latest version
    id "eu.xenit.docker-compose.auto" version "5.5.0" apply(false) // See https://plugins.gradle.org/plugin/eu.xenit.docker-compose.auto for the latest version
}

group = 'eu.xenit.alfresco'
description = "Hotfix for ALF-22094, reduce access logs from Solr for an idle Alfresco"

configure(subprojects.findAll {it.name.contains("hotfix")}) {
    apply plugin: 'java'
    apply plugin: 'eu.xenit.alfresco'
    apply plugin: 'eu.xenit.amp'
    apply plugin: "eu.xenit.docker-alfresco"
    apply plugin: "eu.xenit.docker-compose.auto"
    apply plugin: "signing"
    apply plugin: "maven-publish"

    description 'amp that creates dummy transactions'
    version = rootProject.version
    group = rootProject.group

    apply from: "${rootProject.projectDir}/publishing.gradle"
    apply from: "${rootProject.projectDir}/signing.gradle"
    apply from: "${project.projectDir}/overload.gradle"

    sourceSets {
        main {
            java {
                srcDirs += ["../core/src/main/java"]
            }
        }
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dockerAlfresco {
        baseImage = "${alfrescoImageDep}"
        leanImage = true
        dockerBuild {
            repository = 'alfresco-with-amp'
            automaticTags = true
        }
    }

    dockerCompose {
        useComposeFiles = ['../docker/docker-compose.yml']
        stopContainers = true
        waitForTcpPorts = false
    }

    dependencies {
        alfrescoProvided "${alfRepoDep}"
        alfrescoProvided "${quartz}"
        baseAlfrescoWar "${alfrescoWarDep}"
        alfrescoAmp 'org.orderofthebee.support-tools:support-tools-repo:1.2.2.0:amp@amp'
        alfrescoAmp project.tasks.amp.outputs.files
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
    }
}




