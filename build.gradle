plugins {
    id 'eu.xenit.alfresco' version "1.0.1" apply(false) // Have a look at https://plugins.gradle.org/plugin/eu.xenit.alfresco for the latest version
    id 'eu.xenit.amp' version "1.0.1" apply(false)
    id "eu.xenit.docker-alfresco" version "5.0.5" apply(false) // See https://plugins.gradle.org/plugin/eu.xenit.docker-alfresco for the latest version
    id "eu.xenit.docker-compose.auto" version "5.0.5" apply(false) // See https://plugins.gradle.org/plugin/eu.xenit.docker-compose.auto for the latest version
}

group 'eu.xenit.alfresco'
description = "Hotfix for ALF-22094, reduce access logs from Solr for an idle Alfresco"

def baseVersion = "1.0.0"

def branchName = System.env.BRANCH_NAME
def isRelease = branchName != null && branchName.startsWith("release")
if (isRelease)
    version = "$baseVersion"
else
    version = "$baseVersion-SNAPSHOT"

configure(subprojects.findAll {it.name.contains("hotfix")}) {
    apply plugin: 'java'
    apply plugin: 'eu.xenit.alfresco'
    apply plugin: 'eu.xenit.amp'
    apply plugin: "eu.xenit.docker-alfresco"
    apply plugin: "eu.xenit.docker-compose.auto"
    apply plugin: "signing"
    apply plugin: "maven-publish"

    description 'amp that creates dummy transactions'
    version = rootProject.version
    group = rootProject.group

    apply from: "${rootProject.projectDir}/publishing.gradle"
    apply from: "${rootProject.projectDir}/signing.gradle"
    apply from: "${project.projectDir}/overload.gradle"

    sourceSets {
        main {
            java {
                srcDirs += ["../core/src/main/java"]
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dockerAlfresco {
        baseImage = "${alfrescoImageDep}"
        leanImage = true
        dockerBuild {
            repository = 'alfresco-with-amp'
            automaticTags = true
        }
    }

    dockerCompose {
        useComposeFiles = ['../docker/docker-compose.yml']
        stopContainers = true
        waitForTcpPorts = false
    }

    dependencies {
        alfrescoProvided("${alfRepoDep}")
        baseAlfrescoWar "${alfrescoWarDep}"
        alfrescoAmp tasks.getByPath("${ampTask}").outputs.files
    }
}

repositories {
    mavenCentral()
}




